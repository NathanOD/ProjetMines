buildscript {
    repositories {
        maven {
            url = uri("https://plugins.gradle.org/m2/")
        }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.2'
    }
}

plugins {
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id "org.beryx.jlink" version "2.26.0"
    id 'com.google.protobuf'
}

group 'fr.sos.projetmines.gui'
version '1.0'
description = "Projet Mines x ArcelorMittal - Orowan - GUI module"
project.ext.set('grpcVersion', '1.51.0')
project.ext.set('protobufVersion', '3.22.2')
project.ext.set('protocVersion', project.ext.get('protobufVersion'))

repositories {
    mavenCentral()
}

dependencies {
    //gRPC
    implementation "io.grpc:grpc-protobuf:${project.ext.get('grpcVersion')}"
    implementation "io.grpc:grpc-stub:${project.ext.get('grpcVersion')}"
    //implementation "io.grpc:grpc-netty:${project.ext.get('grpcVersion')}"
    implementation "io.grpc:grpc-netty:${project.ext.get('grpcVersion')}"
    //Conflicts in package finding for module generation forced to merge grcp-api and grcp-context files
    implementation("org.rationalityfrontline.workaround:grpc-api:1.51.0")
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}


//Prevent any compiling of grpc-api and grpc-context
configurations.all {
    exclude group: "io.grpc", module: "grpc-context"
    exclude group: "io.grpc", module: "grpc-api"
}

// GRPC
protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${project.ext.get('protocVersion')}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${project.ext.get('grpcVersion')}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        proto {
            srcDir '../grpc-proto'
        }
        java {

        }
    }
}

javafx {
    version = "19"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

//Archive generation
shadowJar {
    archiveClassifier.set('GUI')
}

jar {
    manifest {
        attributes 'Main-Class': 'fr.sos.projetmines.gui.OrowanGUI'
    }
}

application {
    mainClass = 'fr.sos.projetmines.gui.OrowanGUI'
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'Projet.Mines.ProjetMinesSOS.OrowanGUI'
    }
}