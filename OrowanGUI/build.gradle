buildscript {
    repositories { maven { url = uri("https://plugins.gradle.org/m2/") } }
    dependencies { classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.2' }
}

plugins {
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id "org.beryx.jlink" version "2.26.0"
    id("com.github.johnrengelman.shadow") version "7.1.2"
}
apply plugin: 'com.google.protobuf'

group 'fr.sos.projetmines.gui'
version '1.0'
description = "Projet Mines x ArcelorMittal - Orowan - GUI module"

// -------------------- DEPENDENCIES --------------------

repositories {
    mavenCentral()
}

dependencies {
    //gRPC
    implementation "io.grpc:grpc-protobuf:1.51.0"
    implementation "io.grpc:grpc-stub:1.51.0"
    implementation "io.grpc:grpc-netty:1.51.0"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation("org.rationalityfrontline.workaround:grpc-api:1.51.0")
}

// -------------------- GRPC CONFIGURATION --------------------

//Prevent any compiling of grpc-api and grpc-context, use org.rationalityfrontline.workaround:grpc-api instead
configurations.all {
    exclude group: "io.grpc", module: "grpc-context"
    exclude group: "io.grpc", module: "grpc-api"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:3.22.2" }
    plugins { grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.51.0" } }
    generateProtoTasks { all()*.plugins { grpc {} } }
}

sourceSets { main { proto { srcDir '../grpc-proto' } } }

// -------------------- ARCHIVE GENERATION --------------------

jar {
    manifest {
        attributes 'Main-Class': 'fr.sos.projetmines.gui.OrowanGUI'
    }
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'Projet.Mines.ProjetMinesSOS.OrowanGUI'
    }
}

application {
    mainClass = 'fr.sos.projetmines.gui.OrowanGUI'
}

shadowJar {
    archiveClassifier.set('GUI')
    archiveBaseName.set('Mines-x-ArcelorMittal_Orowan')
}

// -------------------- JAVAFX SUPPORT --------------------

javafx {
    version = "19"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}
