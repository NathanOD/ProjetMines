buildscript {
    repositories { maven { url = uri("https://plugins.gradle.org/m2/") } }
    dependencies { classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.2' }
}

plugins {
    id("com.github.johnrengelman.shadow") version "7.1.2"
}
apply plugin: 'com.google.protobuf'

group 'fr.sos.projetmines.databasenotifier'
version '1.0'
description = "Projet Mines x ArcelorMittal - Orowan - Datbase notifier module"

// -------------------- DEPENDENCIES --------------------

repositories {
    mavenCentral()
}

dependencies {
    //gRPC
    implementation "io.grpc:grpc-protobuf:1.51.0"
    implementation "io.grpc:grpc-services:1.51.0"
    implementation "io.grpc:grpc-netty:1.51.0"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation("org.rationalityfrontline.workaround:grpc-api:1.51.0")

    //Common utils
    implementation project(':CommonUtils')

    // H2 Database driver
    implementation 'com.h2database:h2:2.1.214'
}

// -------------------- GRPC CONFIGURATION --------------------

//Prevent any compiling of grpc-api and grpc-context, use org.rationalityfrontline.workaround:grpc-api instead
configurations.all {
    exclude group: "io.grpc", module: "grpc-context"
    exclude group: "io.grpc", module: "grpc-api"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:3.22.2" }
    plugins { grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.51.0" } }
    generateProtoTasks { all()*.plugins { grpc {} } }
}

// -------------------- ARCHIVE GENERATION --------------------

//Archive generation
shadowJar {
    archiveClassifier.set('DatabaseNotifier')
    archiveBaseName.set('Mines-x-ArcelorMittal_Orowan')
    doLast {
        copy {
            from shadowJar
            into project.databaseClasspathFolder
            println "Copied generated FAT JAR to " + project.databaseClasspathFolder
        }
    }
}